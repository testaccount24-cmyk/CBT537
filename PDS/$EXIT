         MACRO
&NAME    $EXIT  &REGS=(YES,NUM),   EQUATES AVAILABLE R0-RF EQU 0-15    X
               &SAVE=SAVE,        SAVE REGISTER EQUATE (REG 13)        X
               &SIZE=,            SAVE AREA SIZE (TO BE FREE'D)        X
               &RC=0              RETURN CODE (DECIMAL), OR            X
                                  &RC=(REG), OR &RC='LABEL'
.*
.*
         LCLA  &R0
         LCLA  &R1
         LCLA  &M
         LCLA  &N
.*
         LCLC  &R12
         LCLC  &R13
         LCLC  &R14
         LCLC  &R15
         LCLC  &R
         LCLC  &RCD
         LCLC  &SAV
.*
.*
&R0      SETA  0
&R1      SETA  1
.*
&R12     SETC  '12'
&R13     SETC  '13'
&R14     SETC  '14'
&R15     SETC  '15'
&R       SETC  ''
.*
.*
.*
         AIF   (K'&NAME LE 0).NONM
.*
&NAME    DS    0H
.*
.*
.NONM    AIF   ('&REGS(1)' EQ 'NO').SRC
.*
&R       SETC  'R'
         AIF   ('&REGS(2)' EQ 'NUM').SRC
.*
&R12     SETC  'C'
&R13     SETC  'D'
&R14     SETC  'E'
&R15     SETC  'F'
.*
.*
.SRC     AIF   (T'&RC EQ 'O').SREG
         AIF   (T'&RC EQ 'F').FWRC
         AIF   (T'&RC EQ 'H').HWRC
         AIF   (T'&RC EQ 'C').M255
         AIF   (T'&RC EQ 'N').SDRC
         AIF   (T'&RC NE 'U').SREG
.*
         LR    &R.&R15,&RC(1)              LOAD RETURN CODE
         AGO   .SREG
.*
.*
.SDRC    LA    &R.&R15,&RC             SET RETURN CODE
         AGO   .SREG
.*
.*
.M255    SR    &R.&R15,&R.&R15              LOAD RETURN
         IC    &R.&R15,&RC                CODE (MODULO 255)
         AGO   .SREG
.*
.*
.HWRC    LH    &R.&R15,&RC             LOAD RETURN CODE
         AGO   .SREG
.*
.*
.FWRC    L     &R.&R15,&RC             LOAD RETURN CODE
.SREG    ANOP
&SAV     SETC  '&R.&R13'
         AIF   (K'&SAVE EQ 0).NSAV
.*
&SAV     SETC  '&SAVE'
.*
.*
.NSAV    AIF   (K'&SIZE EQ 0).NSIZ
.*
         LR    &R.&R0,&SAV        TEMP SAVE POINTER
.*
.*
.NSIZ    L     &SAV,4(,&SAV)      RESTORE OLD SAVE POINTER
         AIF   (K'&SIZE EQ 0).RS14
.*
         ST    &R.&R1,24(,&SAV)         PRESERVE PARM POINTER
         LR    &R.&R1,&R.&R0            ADDR FOR FREEMAIN
         L     &R.&R0,&SIZE            SIZE OF FREEMAIN
         LA    &R.&R1,0(,&R.&R1)               FREE THE
         SVC   10                      SAVE AREA
.*
.*
.RS14    L     &R.&R14,12(,&SAV)       RESTORE RETURN ADDRESS
.*
.*
.DONE    LM    &R.&R0,&R.&R12,20(&SAV)       RESTORE REGISTERS
         BR    &R.&R14                 RETURN
         SPACE 3
         MEND
